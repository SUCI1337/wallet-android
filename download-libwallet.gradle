/**
 * Downloads JNI binaries as version specified in the project-level gradle file.
 */
task downloadLibwallet  {
    final def binariesDir = "${rootProject.projectDir}/libwallet/"
    inputs.property "version", libwalletVersion
    outputs.dir binariesDir
    doLast {
        logger.info("Downloading binaries with version $libwalletVersion")
        final def binaryArchives = supportedABIs.collect {
            "libwallet-${it}-${libwalletVersion}.tar.gz"
        }
        delete {
            delete fileTree(binariesDir)
        }
        download {
            src(binaryArchives.collect { "$libwalletHostURL$it" })
            dest binariesDir
        }
        copy {
            binaryArchives.collect { "$binariesDir$it" }.each { from tarTree(it) }
            into binariesDir
        }
        delete {
            delete fileTree(binariesDir) {
                include '**/*.tar.gz'
            }
        }
    }
}
