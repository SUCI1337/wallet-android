import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    ext.kotlin_version = "1.6.10"

    // build & version
    ext.buildNumber = 190
    ext.versionNumber = "0.10.2"

    // JNI libs
    ext.libwalletVersion = "0.28.0"
    ext.libwalletHostURL = "https://tari-binaries.s3.amazonaws.com/libwallet/"
    ext.supportedABIs = ["arm64-v8a", "x86_64"]

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.sentry:sentry-android-gradle-plugin:2.1.5"
    }
}

plugins {
    id "de.undercouch.download" version "4.0.4"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://raw.githubusercontent.com/guardianproject/gpmaven/master" }
    }
}

afterEvaluate {
    subprojects.each { subproject ->
        subproject.tasks.withType(Test) {
            testLogging {
                showStandardStreams = true
                showExceptions true
                showCauses true
                showStackTraces true
                exceptionFormat 'short'
                events TestLogEvent.FAILED, TestLogEvent.SKIPPED
                debug {
                    exceptionFormat 'full'
                    events TestLogEvent.FAILED,
                            TestLogEvent.PASSED,
                            TestLogEvent.SKIPPED,
                            TestLogEvent.STANDARD_OUT
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
